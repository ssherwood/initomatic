plugins {
    id 'base'
}

// make the default "packaged" plugin location the build/plugins folder of this project
ext.pluginsDir = buildDir.path + '/plugins'

subprojects {
    apply plugin: 'java'

    jar {
        // derive these values from each projects gradle.properties
        manifest {
            attributes 'Plugin-Id': "${pluginId}",
                    'Plugin-Description': "${pluginDescription}",
                    'Plugin-Class': "${pluginClass}",
                    'Plugin-Version': "${version}",
                    'Plugin-Provider': "${pluginProvider}",
                    'Plugin-Dependencies': "${pluginDependencies}",
                    'Plugin-License': "${pluginLicense}"
        }
    }

    task plugin(type: Jar) {
        baseName = "initomatic-${pluginId}"

        manifest {
            // use the generated manifest from the jar task
            from file("${buildDir}/tmp/jar/MANIFEST.MF")
        }

        into('lib') {
            from configurations.compile
        }

        into('classes') {
            with jar
        }

        extension('zip')
    }

    task assemblePlugin(type: Copy) {
        from plugin
        into pluginsDir
    }
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

build.dependsOn project.tasks.assemblePlugins