// packaged plugin location
ext.pluginsDir = buildDir.path + '/plugins'

subprojects {
    apply plugin: 'java'

    // right now having to do this twice since we end up with 2 manifests
    // in the tmp output folder and sometimes this confuses pf4j when it
    // finds the empty one
    jar {
        manifest {
            attributes 'Plugin-Class': "${pluginClass}",
                    'Plugin-Id': "${pluginId}",
                    'Plugin-Version': "${version}",
                    'Plugin-Provider': "${pluginProvider}"
        }
    }

    task plugin(type: Jar) {
        baseName = "plugin-${pluginId}"
        manifest {
            from(file("${buildDir}/tmp/jar/MANIFEST.MF"))
        }
        into('classes') {
            with jar
        }
        into('lib') {
            from configurations.compile
        }
        extension('zip')
    }

    task assemblePlugin(type: Copy) {
        from plugin
        into pluginsDir
    }
}

apply plugin: 'java'

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

build.dependsOn project.tasks.assemblePlugins