plugins {
  id 'java'
  id "com.moowork.node" version "1.2.0"
}

node {
  version = '8.11.2'
  npmVersion = '5.6.0'
  download = true
}

clean {
  delete 'dist'
}

jar {
  from('dist/initomatic') {
    into('static')
  }
}

task ngBuild(type: NpmTask, group: 'Angular ng') {
  description = 'Compile client side folder for development'
  args = ['run', 'build']

  onlyIf {
    // don't ng build if dist exists
    !file("${projectDir}/dist").exists()
  }
}

task ngLint(type: NpmTask, group: 'Angular ng') {
  description = 'Run lint checks against client'
  args = ['run', 'lint']
}

task ngEndToEnd(type: NpmTask, group: 'Angular ng') {
  description = 'Run lint checks against client'
  args = ['run', 'e2e']
}

processResources.dependsOn tasks.npmInstall
jar.dependsOn tasks.ngBuild
test.dependsOn tasks.ngLint
build.dependsOn tasks.ngEndToEnd
