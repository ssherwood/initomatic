buildscript {
    ext {
        kotlinVersion = '1.2.41'
        springBootVersion = '2.0.2.RELEASE'
        pf4jVersion = '2.2.0'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath 'net.nemerosa:versioning:2.6.1'
    }
}

defaultTasks "clean", "build"

// apply this configuration to all subprojects
subprojects {
    repositories {
        // mavenLocal()
        jcenter()
        mavenCentral()
    }

    // don't apply plugins to 'container' folders to avoid /build outputs
    if ([':applications', ':components', ':plugins', ':shared'].contains(path)) {
        // okay, this caused an issue where I had to name a real folder pluginz... sigh...
        //println("Skipping >>>> $path")
        return
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'net.nemerosa.versioning'

    group = 'io.undertree.initomatic'
    version = versioning.info.full
    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
