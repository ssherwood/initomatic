buildscript {
    ext {
        // downgrade due to error: https://discuss.kotlinlang.org/t/kotlin-1-2-50-causing-java-lang-illegalstateexception-scriptingcompilerconfigurationcomponentregistrar-is-not-compatible-with-this-version-of-compiler/8157/8
        kotlinVersion = '1.2.41'
        springBootVersion = '2.0.3.RELEASE'
        versioningVersion = '2.7.1'
        pf4jVersion = '2.3.0'
        pf4jUpdateVersion = '2.0.0'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "net.nemerosa:versioning:${versioningVersion}"
    }
}

apply plugin: 'idea'
apply plugin: 'base'
defaultTasks "clean", "build"

// apply this common configuration to all subprojects
subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'net.nemerosa.versioning'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.undertree.initomatic'
    version = versioning.info.full

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
